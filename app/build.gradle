plugins {
    id 'com.android.application'
    id 'com.palantir.git-version' version '0.12.3'
}

def versionDets = versionDetails();
String version;
if (versionDets.isCleanTag) {
    version = versionDets.lastTag;
} else {
    version = "${versionDets.lastTag}+${versionDets.commitDistance}#${versionDets.gitHash}"
}

logger.info("version = $version")

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "ms.appcenter.sampleapp.android"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 2
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Obtain the value of APPCENTER_APP_SECRET environment variable from Build time.
        buildConfigField("String", "APPCENTER_APP_SECRET", "\"\"")
        if (System.getenv("APPCENTER_APP_SECRET") != null) {
            buildConfigField("String", "APPCENTER_APP_SECRET", "\"${System.getenv("APPCENTER_APP_SECRET")}\"")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'

    def appCenterSdkVersion = '2.5.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    // Unit Testing
    testImplementation 'androidx.test.ext:junit:1.1.1'

    // Espresso UI Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.microsoft.appcenter:espresso-test-extension:1.4')
}
